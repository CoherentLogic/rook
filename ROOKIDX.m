ROOKIDX
 Q
 ;
GETROWS(EGG,EMBRYO,VALUE,RESULT)
 ;
 ; SUBROUTINE DATA
 ; 
 N DATA,ERRCT,INDEXED,NEST,ENUM,GLVN,RCNT,TIMER,QTIME S (ERRCT,RCNT)=0
 S TIMER=$$BTIME^ROOK
 ;
 ; REQUIRE CONNECTION TO ROOK
 ;
 I '$$CONNECTED^ROOK D
 . D ERR^ROOK(13,.RESULT)
 . S ERRCT=$I(ERRCT)
 G:ERRCT>0 ERGETROWS
 S RKRESULT=1
 ;
 ; REQUIRE SELECTED NEST
 ; (SUCCESS HERE IMPLIES THAT THE NEST EXISTS)
 ;
 S NEST=$$CURRENTNEST^ROOK
 I NEST="" D
 . D ERR^ROOK(10,.RESULT)
 . S ERRCT=$I(ERRCT)
 G:ERRCT>0 ERGETROWS
 S RKRESULT=1
 S GLVN="^"_NEST
 ;
 ; MAKE SURE EGG EXISTS 
 ;
 S RET=$$EGGEXISTS^ROOKDD(EGG,.RES)
 I RET=0 S ERRCT=$I(ERRCT) D ERR^ROOK(12,.RESULT)
 G:ERRCT>0 ERGETROWS 
 S RKRESULT=1
 ; 
 ; MAKE SURE EMBRYO EXISTS
 ;
 K RET,RES
 S RET=$$EMBRYOEXISTS^ROOKDD(EGG,EMBRYO,.RES)
 I RET=0 S ERRCT=$I(ERRCT) D ERR^ROOK(9,.RESULT)
 G:ERRCT>0 ERGETROWS
 S RKRESULT=1
 ;
 ; FIND OUT IF THE EMBRYO IS INDEXED
 ;
 S INDEXED=$$INDEXED^ROOKDICT(EGG,EMBRYO)
 ;
 ; IF EMBRYO IS INDEXED, USE INDEX SCANNING TO GET ROWS INTO DATA ARRAY
 ;
 N DVAL S DVAL=""
 I INDEXED D
 . S ENUM=""
 . F  S ENUM=$O(@GLVN@("INDEX",EGG,EMBRYO,VALUE,ENUM)) Q:ENUM=""  D
 . . M DATA(EGG,ENUM)=@GLVN@("DATA",EGG,ENUM)
 . . S RCNT=$I(RCNT)
 E  D
 . ; 
 . ; OTHERWISE, DO A LINEAR TABLE SCAN
 . ;
 . S ENUM=""
 . F  S ENUM=$O(@GLVN@("DATA",EGG,ENUM)) Q:ENUM=""  D
 . . S DVAL=@GLVN@("DATA",EGG,ENUM,EMBRYO)
 . . I DVAL=VALUE D 
 . . . M DATA(EGG,ENUM)=@GLVN@("DATA",EGG,ENUM) 
 . . . S RCNT=$I(RCNT)
 S QTIME=$$ETIME^ROOK(TIMER)
 D SUCCESS^ROOK(.RESULT,0,QTIME,RCNT,.DATA)
ERGETROWS
 Q RKRESULT