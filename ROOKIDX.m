ROOKIDX
 Q
 ;
GETROWS(EGG,EMBRYO,VALUE,RESULT)
 ;
 ; SUBROUTINE DATA
 ; 
 N DATA,ERRCT,INDEXED,NEST,ENUM,GLVN,RCNT,TIMER,QTIME,CACHED S (ERRCT,RCNT,CACHED)=0
 S TIMER=$$BTIME^ROOK
 ;
 ; REQUIRE CONNECTION TO ROOK
 ;
 I '$$CONNECTED^ROOK D
 . D ERR^ROOK(13,.RESULT)
 . S ERRCT=$I(ERRCT)
 G:ERRCT>0 ERGETROWS
 S RKRESULT=1
 ;
 ; REQUIRE SELECTED NEST
 ; (SUCCESS HERE IMPLIES THAT THE NEST EXISTS)
 ;
 S NEST=$$CURRENTNEST^ROOK
 I NEST="" D
 . D ERR^ROOK(10,.RESULT)
 . S ERRCT=$I(ERRCT)
 G:ERRCT>0 ERGETROWS
 S RKRESULT=1
 S GLVN="^"_NEST
 ;
 ; CHECK IF THIS QUERY IS CACHED
 ; 
 I $D(RKQCACHE(EGG,EMBRYO,VALUE)) D
 . M DATA=RKQCACHE(EGG,EMBRYO,VALUE)
 . S CACHED=1
 . D SET^ROOKSESSION("CACHE_HITS_MEMORY",$$GET^ROOKSESSION("CACHE_HITS_MEMORY")+1)
 E  D
 . I $D(^RKQCACHE(EGG,EMBRYO,VALUE)) D
 . . M DATA=^RKQCACHE(EGG,EMBRYO,VALUE)
 . . M RKQCACHE(EGG,EMBRYO,VALUE)=^RKQCACHE(EGG,EMBRYO,VALUE)
 . . S CACHED=1
 . . D SET^ROOKSESSION("CACHE_HITS_DISK",$$GET^ROOKSESSION("CACHE_HITS_DISK")+1)
 . E  D
 . . S CACHED=0
 . . D SET^ROOKSESSION("CACHE_MISSES",$$GET^ROOKSESSION("CACHE_MISSES")+1)
 G:CACHED=1 QCACHED
 ;
 ; MAKE SURE EGG EXISTS 
 ;
 S RET=$$EGGEXISTS^ROOKDD(EGG,.RES)
 I RET=0 S ERRCT=$I(ERRCT) D ERR^ROOK(12,.RESULT)
 G:ERRCT>0 ERGETROWS 
 S RKRESULT=1
 ; 
 ; MAKE SURE EMBRYO EXISTS
 ;
 K RET,RES
 S RET=$$EMBRYOEXISTS^ROOKDD(EGG,EMBRYO,.RES)
 I RET=0 S ERRCT=$I(ERRCT) D ERR^ROOK(9,.RESULT)
 G:ERRCT>0 ERGETROWS
 S RKRESULT=1
 ;
 ; FIND OUT IF THE EMBRYO IS INDEXED
 ;
 S INDEXED=$$INDEXED^ROOKDICT(EGG,EMBRYO)
 ;
 ; IF EMBRYO IS INDEXED, USE INDEX SCANNING TO GET ROWS INTO DATA ARRAY
 ;
 N DVAL S DVAL=""
 I INDEXED D
 . S ENUM=""
 . F  S ENUM=$O(@GLVN@("INDEX",EGG,EMBRYO,VALUE,ENUM)) Q:ENUM=""  D
 . . M DATA(EGG,ENUM)=@GLVN@("DATA",EGG,ENUM)
 . . S RCNT=$I(RCNT)
 E  D
 . ; 
 . ; OTHERWISE, DO A LINEAR TABLE SCAN
 . ;
 . S ENUM=""
 . F  S ENUM=$O(@GLVN@("DATA",EGG,ENUM)) Q:ENUM=""  D
 . . S DVAL=@GLVN@("DATA",EGG,ENUM,EMBRYO)
 . . I DVAL=VALUE D 
 . . . M DATA(EGG,ENUM)=@GLVN@("DATA",EGG,ENUM) 
 . . . S RCNT=$I(RCNT)
 M RKQCACHE(EGG,EMBRYO,VALUE)=DATA
QCACHED
 S QTIME=$$ETIME^ROOK(TIMER)
 D SUCCESS^ROOK(.RESULT,0,QTIME,RCNT,.DATA)
ERGETROWS
 Q RKRESULT

